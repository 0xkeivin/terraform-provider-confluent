---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "confluent_private_link_access_v1 Resource - terraform-provider-confluent"
subcategory: ""
description: |-
  
---

# confluent_private_link_access_v1 Resource

<img src="https://img.shields.io/badge/Lifecycle%20Stage-Public%20Preview-%2300afba" alt="">

`confluent_private_link_access_v1` provides a Private Link Access resource that enables creating and deleting access to PrivateLink endpoints by AWS account or Azure subscription.

## Example Usage

### Example Private Link Access on AWS

```terraform
resource "confluent_environment_v2" "development" {
  display_name = "Development"
}

resource "confluent_network_v1" "aws-private-link" {
  display_name = "AWS Private Link Network"
  cloud = "AWS"
  region = "us-east-2"
  connection_types = ["PRIVATELINK"]
  environment {
    id = confluent_environment_v2.development.id
  }
}

resource "confluent_private_link_access_v1" "aws" {
  display_name = "AWS Private Link Access"
  aws {
    account = "012345678901"
  }
  environment {
    id = confluent_environment_v2.development.id
  }
  network {
    id = confluent_network_v1.aws-private-link.id
  }
}
```

### Example Private Link Access on Azure

```terraform
resource "confluent_environment_v2" "development" {
  display_name = "Development"
}

resource "confluent_network_v1" "azure-private-link" {
  display_name     = "Azure Private Link Network"
  cloud            = "AZURE"
  region           = "centralus"
  connection_types = ["PRIVATELINK"]
  environment {
    id = confluent_environment_v2.development.id
  }
}

resource "confluent_private_link_access_v1" "azure" {
  display_name = "Azure Private Link Access"
  azure {
    subscription = "1234abcd-12ab-34cd-1234-123456abcdef"
  }
  environment {
    id = confluent_environment_v2.development.id
  }
  network {
    id = confluent_network_v1.azure-private-link.id
  }
}
```

<!-- schema generated by tfplugindocs -->
## Argument Reference

The following arguments are supported:

- `display_name` - (Optional String) The name of the Private Link Access.
- `environment` (Required Configuration Block) supports the following:
    - `id` - (Required String) The ID of the Environment that the Private Link Access belongs to, for example, `env-abc123`.
- `network` (Required Configuration Block) supports the following:
    - `id` - (Required String) The ID of the Network that the Private Link Access belongs to, for example, `n-abc123`.
- `aws` - (Optional Configuration Block) The AWS-specific Private Link Access details if available. It supports the following:
    - `account` - (Required String) The AWS account ID to enable for the Private Link Access. You can find your AWS account ID [here] (https://console.aws.amazon.com/billing/home?#/account) under **My Account** in your AWS Management Console. Must be a **12 character string**.
- `azure` - (Optional Configuration Block) The Azure-specific Private Link Access details if available. It supports the following:
    - `subscription` - (Required String) The Azure subscription ID to enable for the Private Link Access. You can find your Azure subscription ID in the subscription section of your [Microsoft Azure Portal] (https://portal.azure.com/#blade/Microsoft_Azure_Billing/SubscriptionsBlade). Must be a valid **32 character UUID string**.

-> **Note:** Exactly one from the `aws`, `azure` configuration blocks must be specified.

-> **Note:** Learn more about Private Link Access limitations on AWS [here](https://docs.confluent.io/cloud/current/networking/private-links/aws-privatelink.html#limitations).

-> **Note:** Learn more about Private Link Access limitations on Azure [here](https://docs.confluent.io/cloud/current/networking/private-links/azure-privatelink.html#limitations).

## Attributes Reference

In addition to the preceding arguments, the following attributes are exported:

- `id` - (Required String) The ID of the Private Link Access, for example, `pla-abc123`.

## Import

-> **Note:** `CONFLUENT_CLOUD_API_KEY` and `CONFLUENT_CLOUD_API_SECRET` environment variables must be set before importing a Private Link Access.

You can import a Private Link Access by using Environment ID and Private Link Access ID, in the format `<Environment ID>/<Private Link Access ID>`. The following example shows how to import a Private Link Access:

```shell
$ export CONFLUENT_CLOUD_API_KEY="<cloud_api_key>"
$ export CONFLUENT_CLOUD_API_SECRET="<cloud_api_secret>"
$ terraform import confluent_private_link_access_v1.my_pla env-abc123/pla-abc123
```

!> **Warning:** Do not forget to delete terminal command history afterwards for security purposes.

## Getting Started
The following end-to-end examples might help to get started with `confluent_private_link_access_v1` resource:
  * [`dedicated-privatelink-aws-kafka-acls`](https://github.com/confluentinc/terraform-provider-confluent/tree/master/examples/configurations/dedicated-privatelink-aws-kafka-acls): _Dedicated_ Kafka cluster on AWS that is accessible via PrivateLink connections with authorization using ACLs
  * [`dedicated-privatelink-aws-kafka-rbac`](https://github.com/confluentinc/terraform-provider-confluent/tree/master/examples/configurations/dedicated-privatelink-aws-kafka-rbac): _Dedicated_ Kafka cluster on AWS that is accessible via PrivateLink connections with authorization using RBAC
  * [`dedicated-privatelink-azure-kafka-rbac`](https://github.com/confluentinc/terraform-provider-confluent/tree/master/examples/configurations/dedicated-privatelink-azure-kafka-rbac): _Dedicated_ Kafka cluster on Azure that is accessible via PrivateLink connections with authorization using RBAC
  * [`dedicated-privatelink-azure-kafka-acls`](https://github.com/confluentinc/terraform-provider-confluent/tree/master/examples/configurations/dedicated-privatelink-azure-kafka-acls): _Dedicated_ Kafka cluster on Azure that is accessible via PrivateLink connections with authorization using ACLs
